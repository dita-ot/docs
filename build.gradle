plugins {
    id 'io.github.jyjeanne.dita-ot-gradle' version '2.2.0'
    id 'com.github.eerohele.saxon-gradle' version '0.9.0-beta4'
}

// Specify Saxon version per https://github.com/eerohele/saxon-gradle/blob/master/README.md

repositories {
    mavenCentral()
}

configurations {
    saxon
}

dependencies {
    // Use Saxon-HE 10.6 like DITA-OT, instead of the version that comes with `saxon-gradle`
    saxon 'net.sf.saxon:Saxon-HE:10.6'
}

import com.github.jyjeanne.DitaOtTask
import com.github.eerohele.SaxonXsltTask
import org.gradle.process.ExecOperations

def getPropertyOrDefault(String name, def defaultValue) {
    providers.gradleProperty(name).getOrElse(defaultValue)
}

// Use layout.projectDirectory for configuration cache compatibility
def projectDirPath = layout.projectDirectory.asFile.path

String ditaHome = getPropertyOrDefault('ditaHome', layout.projectDirectory.asFile.getParent())
String ditaHomeSrc = getPropertyOrDefault('ditaHomeSrc', ditaHome)
String configDir = "${ditaHomeSrc}/config"
String ditavalFile = "${projectDirPath}/platform.ditaval"
Boolean toolkitBuild = file("${projectDirPath}/../lib/dost.jar").exists()
String samplesDir = toolkitBuild ? "${ditaHome}/docsrc/samples" : "${projectDirPath}/samples"
String outputDir = getPropertyOrDefault('outputDir', toolkitBuild ? "${ditaHome}/doc" : "${projectDirPath}/out")

String toURI(String path) {
    file(path).toURI().toString()
}

// Set DITA-OT directory: pass as parameter -PditaHome or fall back to parent when run in core repo.
ditaOt.dir ditaHome
// > Could not get unknown property 'ditaOt' for root project 'docs' of type org.gradle.api.Project.

task messages(type: SaxonXsltTask) {
    input "${configDir}/messages.xml"
    output "${projectDirPath}/topics/error-messages.xml"
    stylesheet "${projectDirPath}/resources/messages.xsl"
}

task params(type: SaxonXsltTask) {
    input "${configDir}/plugins.xml"
    output "${projectDirPath}/parameters/all-parameters.dita"
    stylesheet "${projectDirPath}/resources/params.xsl"
    parameters('output-dir.url': toURI('parameters'))
    outputs.dir "${projectDirPath}/parameters"
}

task extensionPoints(type: SaxonXsltTask) {
    input "${configDir}/plugins.xml"
    output "${projectDirPath}/extension-points/all-extension-points.dita"
    stylesheet "${projectDirPath}/resources/extension-points.xsl"
    parameters('output-dir.url': toURI('extension-points'))
    outputs.dir "${projectDirPath}/extension-points"
}

task generatePlatformFilter {
    def outputFile = layout.projectDirectory.file(ditavalFile)
    outputs.file(outputFile)

    doLast {
        // Use Gradle's built-in OS detection instead of Ant
        def platformName = 'unix'  // default
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            platformName = 'windows'
        } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
            platformName = 'mac'
        }

        // Generate the ditaval file using modern Gradle file operations
        outputFile.asFile.text = """<?xml version="1.0" encoding="UTF-8"?>
<val>
    <prop action="include" att="platform" val="${platformName}"/>
    <prop action="exclude" att="platform"/>
</val>
"""
    }
}

task generatePropertiesTemplate(type: SaxonXsltTask) {
    input "${configDir}/plugins.xml"
    output "${samplesDir}/properties/template.properties"
    stylesheet "${projectDirPath}/resources/properties-file.xsl"
}

task autoGenerate(dependsOn: [messages, params, extensionPoints, generatePlatformFilter, generatePropertiesTemplate]) {
    description = 'Run tasks that generate content from resource files and the build environment.'
}

task pdf(type: DitaOtTask, dependsOn: autoGenerate) {
    input "${projectDirPath}/userguide-book.ditamap"
    output outputDir
    transtype 'pdf'
    filter "${projectDirPath}/resources/pdf.ditaval"

    properties {
        property(name: 'args.chapter.layout', value: 'BASIC')
        property(name: 'args.gen.task.lbl', value: 'YES')
        property(name: 'include.rellinks', value: '#default external')
        property(name: 'outputFile.base', value: 'userguide')
        property(name: 'theme', value: "${projectDirPath}/samples/themes/dita-ot-docs-theme.yaml")
    }
}

task html(type: DitaOtTask, dependsOn: autoGenerate) {
    input "${projectDirPath}/userguide.ditamap"
    output outputDir
    transtype 'html5'
    filter "${projectDirPath}/resources/html.ditaval"

    properties {
        property(name: 'args.copycss', value: 'yes')
        property(name: 'args.css', value: 'dita-ot-doc.css')
        property(name: 'args.csspath', value: 'css')
        property(name: 'args.cssroot', value: "${projectDirPath}/resources/")
        property(name: 'args.gen.task.lbl', value: 'YES')
        property(name: 'args.hdr', value: "${projectDirPath}/resources/header.xml")
        property(name: 'args.rellinks', value: 'noparent')
        property(name: 'html5.toc.generate', value: 'no')
        property(name: 'nav-toc', value: 'partial')
    }
}

task htmlhelp(type: DitaOtTask, dependsOn: autoGenerate) {
    input "${projectDirPath}/userguide.ditamap"
    output outputDir
    transtype 'htmlhelp'
    filter ditavalFile

    properties {
        property(name: 'args.copycss', value: 'yes')
        property(name: 'args.css', value: 'dita-ot-doc.css')
        property(name: 'args.csspath', value: 'css')
        property(name: 'args.cssroot', value: "${projectDirPath}/resources/")
        property(name: 'args.gen.task.lbl', value: 'YES')
    }

    doLast {
        // Move .chm files using modern Gradle file operations
        def htmlhelpDir = file("${outputDir}/htmlhelp")
        if (htmlhelpDir.exists()) {
            copy {
                from htmlhelpDir
                into outputDir
                include '*.chm'
            }
            // Clean up the htmlhelp directory
            delete htmlhelpDir
        }
    }
}

task cleanUp {
    doLast {
        delete outputDir
    }
}

// Get git commit hash at configuration time for tasks that need it
def getGitCommitHash() {
    try {
        def result = new ByteArrayOutputStream()
        exec {
            workingDir = layout.projectDirectory.asFile
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = result
            ignoreExitValue = true
        }
        return result.toString().trim()
    } catch (Exception e) {
        logger.warn("Could not get git commit hash: ${e.message}")
        return 'unknown'
    }
}

// Store git commit for use by tasks
def gitCommitHash = getGitCommitHash()

task gitMetadata {
    // This task just logs the git commit for reference
    doLast {
        logger.info("Git commit: ${gitCommitHash}")
    }

    // Mark outputs to help with up-to-date checking
    outputs.upToDateWhen { false } // Always run since git commit changes frequently
}

task site(type: DitaOtTask) {
    dependsOn 'messages', 'params', 'extensionPoints', 'gitMetadata'

    input file("${projectDirPath}/site.ditamap")
    output getPropertyOrDefault('outputDir', "${buildDir}/site")
    filter "${projectDirPath}/resources/site.ditaval"

    transtype 'org.dita-ot.html'

    // Evaluate the noCommitMeta flag at configuration time
    def includeCommitMeta = !providers.gradleProperty('noCommitMeta').map { Boolean.parseBoolean(it) }.getOrElse(false)

    properties {
        property(name: 'args.gen.task.lbl', value: 'YES')
        property(name: 'args.rellinks', value: 'noparent')
        if (includeCommitMeta) {
            // Use the git commit hash obtained at configuration time
            property(name: 'commit', value: gitCommitHash)
        }
    }
}

task all(dependsOn: [pdf, html, htmlhelp])
task dist(dependsOn: [pdf, html])

defaultTasks 'dist'
